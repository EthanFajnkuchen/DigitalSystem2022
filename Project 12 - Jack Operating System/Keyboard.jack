// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack

/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {

    static Array keyboard;

    /** Initializes the keyboard. */
    function void init() 
    {
        let keyboard = 24576;
		return;
    } 

    /**
     * Returns the character of the currently pressed key on the keyboard;
     * if no key is currently pressed, returns 0.
     *
     * Recognizes all ASCII characters, as well as the following keys:
     * new line = 128 = String.newline()
     * backspace = 129 = String.backspace()
     * left arrow = 130
     * up arrow = 131
     * right arrow = 132
     * down arrow = 133
     * home = 134
     * End = 135
     * page up = 136
     * page down = 137
     * insert = 138
     * delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() 
    {
        return keyboard[0];
    }

    /**								
     * Waits until a key is pressed on the keyboard and released,
     * then echoes the key to the screen, and returns the character 
     * of the pressed key.
     */
    function char readChar()
    {
        var int character;
		while (Keyboard.keyPressed() = 0) {} //do nothing
		let character = keyboard[0];
		while (Keyboard.keyPressed() = character) {} //do nothing
		do Output.printChar(character);
		return character;
    }

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its value. Also handles user backspaces.
     */
    function String readLine(String message) 
    {
		var int character;
        var String str;
		do Output.printString(message);
		
		let character = Keyboard.readChar();
		let str = String.new(100);
        while (~(character = String.newLine())) 
        {
			if (~(character = String.backSpace()))
            {
                do str.appendChar(character);
			}
			else 
            {
                do str.eraseLastChar();
			}

			let character = Keyboard.readChar(); // read the new char
		}

		return str; //return the string
    }   

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its integer value (until the first non-digit character in the
     * entered text is detected). Also handles user backspaces. 
     */
    function int readInt(String message) 
    {
        var String str;
		let str = Keyboard.readLine(message);
		return str.intValue();
    }
}
